int h;
int n = 5;
int speed = 1;
int totalBeams = 400;
int beamlength = 1;
int delay = 1;
int seed = 0;

boolean on = true;
int[] values;

Beam[] beams;


void setup() {
  frameRate(200);
  size(1000,600);
  h = floor(height/(n+5));
  values = new int[n+1];
  for (int i=0; i<n+1; i++) {
    values[i] = 0;
  }
  
  randomSeed(seed);
  
  beams = new Beam[totalBeams];
  beams[0] = new Beam(speed, h, n);
  
}



void draw() {
  
  background(20);
  
  drawPoints(n,h);
  drawSquares(n,h);
  
  
  for (int i=0; i<beamlength; i++) {
    beams[i].changeDirection();
    beams[i].update();
    beams[i].show();
  }
  
  if (frameCount % delay == 0 && frameCount != 0 && on) {
    setnewBeam(beamlength);
  }
 
  if (beamlength == totalBeams) {
    on = false;
  }
  
  setValue(values);
  drawValue(values, n, h);

  
}

void drawPoints(int n, int h) {
  
  for (int i=1; i<=n; i++) {
    float y = i*h;
    
    for (int j=1; j<=i; j++) {
      
      float x = width/2-h*(i-1)+2*h*(j-1);
      
      fill(255);
      noStroke();
      ellipse(x,y,3,3);
      
    }
  }
  
}

void drawSquares(int n, int h) {
  
  float y = (n+1)*h;
  for (int i=0; i<=n; i++) {
    
    float x = width/2-h*n+2*h*i;
    rectMode(CENTER);
    
    fill(255,0,10);

    noStroke();
    rect(x,y,10,10);
    
  }
  
}

void setValue(int[] a) {
  int p = checkCollision();
  if (p >= 0) {
    a[p] += 1;
  }
  
}

void drawValue(int[] a, int n, int h) {
  
  float y = (n+2)*h;
  float z = (n+3)*h;
  float zs = (n+4)*h;
  textAlign(CENTER);
  textSize(14);
  fill(255);
  for (int i=0; i<=n; i++) {
    
    float x = width/2-h*n+2*h*i;
    text(a[i],x,y);
    
    float v = a[i]*(pow(2,n))/totalBeams;
    text(v,x,z);
    
    long u1 = fact(n)/fact(i);
    long u = u1 / fact(n-i);
    text(int(u),x,zs);
    
  }
  text(beamlength, width/2-h*n+2*h*(n+1), y);
  
  textAlign(LEFT);
  textSize(20);
  text("Beam:",5,y);
  text("Coefficient:",5,z);
  text("Real value:",5,zs);
  
}

void setnewBeam(int c) {
  
  beams[c] = new Beam(speed, h, n);
  beamlength++;
  
}

int checkCollision() {
  
  int value = -1;
  for (int j=0; j<beamlength; j++) {
    float x = beams[j].x;
    float y = beams[j].y;
    for (int i=0; i<=n; i++) {
    
      if (x == width/2-h*n+2*h*i && y == (n+1)*h) {
        value = i;
      }
    }
  }
  return(value);
}

long fact(int n) {
  if (n == 1 || n == 0) {
    return 1;
  }
  return n*fact(n-1);
}
